repositories {
    jcenter()
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.26.0"
    // additional 3rd party ruleset(s) can be specified here
    // just add them to the classpath (e.g. ktlint 'groupId:artifactId:version') and
    // ktlint will pick them up
    ktlint "me.cassiano:ktlint-html-reporter:0.1.2"
}

def inputFiles = project.fileTree(dir: "src", include: "AndroidTest/*.kt")
def outputDir = "${project.buildDir}/reports/ktlint/"
def outputFile = "${outputDir}ktlint.html"
task ktlint(type: JavaExec, group: "verification") {
    // https://medium.com/@vanniktech/making-your-gradle-tasks-incremental-7f26e4ef09c3
    inputs.files(inputFiles)
    outputs.dir(outputDir)

    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args = [
        "--reporter=plain",
        "--reporter=html,artifact=me.cassiano:ktlint-html-reporter,output=${outputFile}",
        "--android",
        "src/**/*.kt"
    ]
}

afterEvaluate {
    check.dependsOn ktlint
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}
